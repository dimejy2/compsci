(* ML ASSESSED EXERCISES.  TICK 5* SUBMISSION FROM M.A. KLEPPMANN *)
(* Estimated time: 30 mins. Actual time: started 11:05. *)

datatype univ = Int of int
              | Real of real
              | Bool of bool
              | String of string
              | Pair of univ*univ
              | Named of string*univ;

(* uofl creates a univ structure from the argument univ list, using nested
   Pair()s. *)

fun uofl [] = String "nil"
  | uofl (u::us) = Pair(u, uofl us);

(* lofu performs the inverse of uofl. It raises an exception if the univ
   structure is not in the form generated by uofl. *)

fun lofu (String "nil") = []
  | lofu (Pair(u,v)) = u::lofu(v);

(* ints extracts all Int values in an arbitrary univ structure and returns
   them as a list. *)

fun ints (Int n) = [n]
  | ints (Pair(u,v)) = ints u @ ints v
  | ints (Named(s,u)) = ints u
  | ints u = [];

(* addreals returns the sum of all Real values in an arbitrary univ
   structure. *)

fun addreals (Real r) = r
  | addreals (Pair(u,v)) = addreals u + addreals v
  | addreals (Named(s,u)) = addreals u
  | addreals _ = 0.0;
